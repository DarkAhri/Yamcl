buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'groovy'
apply plugin: 'forge'
apply plugin: 'maven'

version = "1.7.10-0.5.65"
group= "eu.usrv" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "YAMCore"

minecraft {
    version = "1.7.10-10.13.2.1291"
    runDir = "eclipse"
	
    replaceIn "src/main/java/eu/usrv/yamcore/YAMCore.java"
    replace "GRADLETOKEN_VERSION", project.version.split('-')[1]
}

dependencies {}

processResources
{
    def cleanVersion = project.version.split('-')[1]
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", cleanVersion
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':cleanVersion, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Create API library jar
task libJar(type: Jar) {
    from(sourceSets.main.java) {
        include "eu/usrv/yamcl/lib/**/*"
    }
    classifier = 'lib'
}

// Create Maven source jar
task sourceJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives deobfJar
    archives libJar
    archives groovydocJar
    archives sourceJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: nexusRepositoryUrl) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'YAMCore'
                packaging 'jar'
                description 'YetAnotherModdingCoreLib'
                url 'http://jenkins.usrv.de/'

                licenses {
                    license {
                        name 'General Public License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}


